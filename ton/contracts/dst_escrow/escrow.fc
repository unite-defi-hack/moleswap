#include "../imports/stdlib.fc";
#include "../imports/op_codes.fc";
#include "../imports/message.fc";
#include "../imports/errors.fc";
#include "../imports/utils.fc";
#include "getters.fc";
#include "storage.fc";

#pragma version >=0.4.4;

const exec_fee = 30000000;  ;; 0.03 TON
const operational_fees = 1000000;  ;; 0.001 TON


() recv_internal(int my_balance, int msg_value, cell in_msg_full, slice in_msg_body) impure {
    if (in_msg_body.slice_empty?()) { ;; ignore empty messages
        return ();
    }
    slice cs = in_msg_full.begin_parse();
    int flags = cs~load_uint(4);
    if (flags & 1) { ;; ignore all bounced messages
        return ();
    }
    slice sender_address = cs~load_msg_addr();

    load_data();

    int op = in_msg_body~load_uint(32);
    int query_id = in_msg_body~load_uint(64);

    if (op == op::create) {
        throw_unless(error::forbidden,
            equal_slice_bits(sender_address, storage::lop_address));

        storage::hashlock = in_msg_body~load_uint(256);
        storage::creation_time = in_msg_body~load_uint(32);
        storage::expiration_time = in_msg_body~load_uint(32);

        storage::execution_start_time = now();
        storage::withdrawal_timelock = in_msg_body~load_uint(32);
        storage::public_withdrawal_timelock = in_msg_body~load_uint(32);
        storage::cancellation_timelock = in_msg_body~load_uint(32);

        slice maker_cs = (in_msg_body~load_ref()).begin_parse();
        storage::maker_addr = maker_cs~load_uint(256);
        storage::maker_asset_addr = maker_cs~load_uint(256);
        storage::maker_asset_amount = maker_cs~load_uint(128);
        storage::receiver_addr = maker_cs~load_msg_addr();

        slice taker_cs = (in_msg_body~load_ref()).begin_parse();
        storage::taker_addr = taker_cs~load_msg_addr();
        storage::taker_asset_addr = taker_cs~load_msg_addr();
        storage::taker_asset_amount = taker_cs~load_coins();

        storage::asset_jetton_address = empty_slice(); ;; used for jettons orders only

        throw_unless(error::expired, now() < storage::expiration_time);
        throw_unless(error::not_ready, now() >= storage::creation_time);

        save_data();
        return ();
    }

    if (op == op::withdraw) {
        throw_unless(error::forbidden,
            equal_slice_bits(sender_address, storage::taker_addr));
        throw_unless(error::withdraw_timelock_not_passed,
            now() >= storage::execution_start_time + storage::withdrawal_timelock);
        throw_unless(error::cancelation_phase_started,
            now() < storage::execution_start_time + storage::cancellation_timelock);

        int secret = in_msg_body~load_uint(256);
        validate_secret(secret, storage::hashlock);

        if (address::is_hole(storage::taker_asset_addr)) {
            destruct_and_pay(storage::receiver_addr);
        } else {
            send_jetton(
                storage::receiver_addr,
                storage::taker_asset_amount,
                storage::taker_asset_addr,
                sender_address,
                0,
                null(),
                0,
                query_id,
                CARRY_REMAINING_BALANCE + SELFDESTRUCT_ON_EMPTY
            );
        }
        return ();
    }

    if (op == op::public_withdraw) {
        ;; ideally this should be allowed for token holders only
        throw_unless(error::public_withdraw_timelock_not_passed,
            now() >= storage::execution_start_time + storage::public_withdrawal_timelock);
        throw_unless(error::cancelation_phase_started,
            now() < storage::execution_start_time + storage::cancellation_timelock);

        int secret = in_msg_body~load_uint(256);
        validate_secret(secret, storage::hashlock);

        if (address::is_hole(storage::taker_asset_addr)) {
            send_ton(storage::receiver_addr, storage::taker_asset_amount + operational_fees);
        } else {
            send_jetton(
                storage::receiver_addr,
                storage::taker_asset_amount,
                storage::taker_asset_addr,
                sender_address,
                0,
                null(),
                exec_fee,
                query_id,
                PAY_FEES_SEPARATELY
            );
        }
        destruct_and_pay(sender_address);
        return ();
    }

    if (op == op::cancel) {
        throw_unless(error::forbidden,
            equal_slice_bits(sender_address, storage::taker_addr));
        throw_unless(error::cancelation_phase_not_started,
            now() >= storage::execution_start_time + storage::cancellation_timelock);

        if (address::is_hole(storage::taker_asset_addr)) {
            destruct_and_pay(storage::taker_addr);
        } else {
            send_jetton(
                storage::taker_addr,
                storage::taker_asset_amount,
                storage::taker_asset_addr,
                sender_address,
                0,
                null(),
                0,
                query_id,
                CARRY_REMAINING_BALANCE + SELFDESTRUCT_ON_EMPTY
            );
        }
        return ();
    }

    throw(0xffff);
}
